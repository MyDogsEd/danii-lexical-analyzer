plugins {
    id "com.gradleup.shadow" version "8.3.1"
    id 'java'
}

group = 'dev.mydogsed'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    // JUnit for testing
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation('org.junit.jupiter:junit-jupiter')

    // JDA
    implementation("net.dv8tion:JDA:5.2.2")

    // Logback for logging
    implementation("ch.qos.logback:logback-classic:1.5.16")

    // Hibernate for database
    implementation platform("org.hibernate.orm:hibernate-platform:6.6.11.Final")
    implementation('org.hibernate.orm:hibernate-core')
    implementation('org.hibernate.orm:hibernate-community-dialects')
    implementation("jakarta.transaction:jakarta.transaction-api")

    // sqlite jdbc driver
    implementation('org.xerial:sqlite-jdbc:3.49.1.0')

    // Mockito for mocking JDA
    testImplementation("org.mockito:mockito-core:4.11.0")
    testImplementation("org.mockito:mockito-junit-jupiter:4.11.0")
}

test {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes "Main-Class": "dev.mydogsed.sollexicalanalyzer.Main"
    }
}

// Set the file name
// Copy the file to the execution directory
shadowJar {
    archiveFileName.set('sol-lexical-analyzer.jar')
    finalizedBy("copyJar")
}

// Copy the .JAR file from the libs directory to the execution directory (Creating it if not present)
tasks.register('copyJar', Copy) {
    from "build/libs/sol-lexical-analyzer.jar"
    into "execution"
}

// Task that builds and then runs the bot jar file
tasks.register("buildAndRunJar", JavaExec){
    dependsOn 'shadowJar'
    dependsOn 'copyJar'
    workingDir = file("execution")
    classpath = files("execution/sol-lexical-analyzer.jar")
}